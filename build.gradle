plugins {
    id 'java'
    id 'java-gradle-plugin'

    id 'com.gradle.plugin-publish' version '0.12.0'

    // for publishing snapshots
    id 'maven-publish'
    id 'org.hibernate.build.maven-repo-auth' version '3.0.4'
}


gradlePlugin {
    plugins {
        plugin {
            id = 'quarkus-poc'
            implementationClass = 'org.hibernate.build.gradle.quarkus.QuarkusPlugin'
        }
    }
}

pluginBundle {
    website = 'http://quarkus.io'
    vcsUrl = 'https://github.com/gradle/gradle'
    description = 'Proof-of-concept Gradle plugin for builds using Quarkus'
    tags = ['quarkus', 'graalvm']

    plugins {
        plugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Proof-of-concept Gradle plugin for builds using Quarkus'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    ext {
        junit5Version = '5.3.1'
    }

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: ext.junit5Version
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: "1.3"

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: ext.junit5Version
}

tasks.test {
    useJUnitPlatform()
}

sourceSets {
    test {
        resources.srcDirs += 'src/test/projects'
    }
}


// For publishing testing snapshots

group 'org.hibernate.build.gradle'
version '1.0.0-SNAPSHOT'

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// for publishing snapshots
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
if ( version.toString().endsWith( '-SNAPSHOT' ) ) {
    tasks.publishPlugins.enabled = false
}

publishing {
    publications {
        publishedArtifacts( MavenPublication ) {
            from components.java
        }
    }

    repositories {
        maven {
            name 'jboss-snapshots-repository'
            url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
        }
    }
}
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


// Configure wrapper to download the full Gradle distribution
wrapper {
    // To upgrade the version of gradle used in the wrapper, run:
    //     ./gradlew wrapper --gradle-version NEW_VERSION
    distributionType = Wrapper.DistributionType.ALL
}