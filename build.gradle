plugins {
    id 'java'
    id 'java-gradle-plugin'

    id 'com.gradle.plugin-publish' version '0.12.0'

    // for publishing snapshots
    id 'maven-publish'
    id 'org.hibernate.build.maven-repo-auth' version '3.0.4'
}

ext {
    pluginId = 'sebersole.quarkus.plugin-poc'
    pluginVersion = '0.0.6.7'
}

// For publishing testing snapshots

group 'com.github.sebersole'
version project.pluginVersion

gradlePlugin {
    plugins {
        plugin {
            id = project.pluginId
            implementationClass = 'com.github.sebersole.gradle.quarkus.QuarkusPlugin'
        }
    }
}

pluginBundle {
    ext {
        pluginUrl = 'https://github.com/sebersole/quarkus-gradle-plugin-poc'
        pluginDescription = 'Proof-of-concept Gradle plugin for builds using Quarkus'
    }
    website = ext.pluginUrl
    vcsUrl = ext.pluginUrl
    description = ext.pluginDescription
    tags = ['quarkus', 'graalvm']

    plugins {
        plugin {
            // id is captured from java-gradle-plugin configuration
            displayName = pluginDescription
        }
    }
}

if ( version.toString().endsWith( '-SNAPSHOT' ) ) {
    tasks.publishPlugins.enabled = false
}
else {
    tasks.publish.enabled = false
}

repositories {
    mavenCentral()
}

dependencies {
    ext {
        junit5Version = '5.3.1'
    }

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: ext.junit5Version
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: "1.3"

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: ext.junit5Version
}

tasks.test {
    useJUnitPlatform()
}

sourceSets {
    test {
        resources.srcDirs += 'src/test/projects'
    }
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// for publishing snapshots
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

publishing {
    publications {
        publishedArtifacts( MavenPublication ) {
            from components.java
        }
    }

    repositories {
        maven {
            name 'jboss-snapshots-repository'
            url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
        }
    }
}
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

javadoc {
    options.addStringOption( 'Xdoclint:none', '-quiet' )
}


// Configure wrapper to download the full Gradle distribution
wrapper {
    // To upgrade the version of gradle used in the wrapper, run:
    //     ./gradlew wrapper --gradle-version NEW_VERSION
    distributionType = Wrapper.DistributionType.ALL
}